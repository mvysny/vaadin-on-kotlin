apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"
apply plugin: 'org.junit.platform.gradle.plugin'

dependencies {
    compile(project(':vok-framework'))
    compile(project(':vok-util-vaadin8'))

    testCompile("com.github.mvysny.dynatest:dynatest:${dynatest_version}")

    testCompile 'javax.validation:validation-api:2.0.0.GA'

    // db
    compile('org.hibernate:hibernate-core:5.2.11.Final') {
        exclude group: 'javax.enterprise'  // there is no need for CDI-API nor @Inject stuff
    }
    testCompile "org.flywaydb:flyway-core:${flyway_version}"
    testCompile 'com.h2database:h2:1.4.196'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework-jpa'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Vaadin-On-Kotlin, a simple way to build webapp with Kotlin and Vaadin')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

