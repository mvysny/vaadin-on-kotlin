defaultTasks 'clean', 'build'

buildscript {
    ext.kotlin_version = '1.1.2-3'
    ext.vaadin_version = '8.1.0.beta1'
    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'com.github.vok'
    version '0.2.1-SNAPSHOT'
}

subprojects {

    apply plugin: 'java'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    apply plugin: 'kotlin'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.1"
            languageVersion = "1.1"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name 'vaadin-addons'
            url 'http://maven.vaadin.com/vaadin-addons'
        }
        maven {
            name 'vaadin-prereleases'
            url 'https://maven.vaadin.com/vaadin-prereleases'
        }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
