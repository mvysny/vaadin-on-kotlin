buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

// don't update Jetty carelessly, it tends to break Atmosphere and Push support!
// test before commit :-)
// see https://github.com/vaadin/framework/issues/8134 for details
ext.jettyVer = '9.4.2.v20170220'

dependencies {
    compile(project(":vok-framework-jpa"))
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // logging
    // currently we are logging through the SLF4J API to LogBack. See logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'
    // this will configure Vaadin to log to SLF4J
    compile 'org.slf4j:jul-to-slf4j:1.7.25'

    // Vaadin
    compile "com.vaadin:vaadin-client-compiled:$vaadin_version"
    compile "com.vaadin:vaadin-server:$vaadin_version"
    compile "com.vaadin:vaadin-push:$vaadin_version"
    compile "com.vaadin:vaadin-themes:$vaadin_version"
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // validation
    compile 'javax.validation:validation-api:1.0.0.GA'  // so that the BeanFieldGroup will perform JSR303 validations
    compile 'org.hibernate:hibernate-validator:5.3.4.Final'
    // http://stackoverflow.com/questions/24386771/javax-validation-validationexception-hv000183-unable-to-load-javax-el-express
    compile 'org.glassfish.web:javax.el:2.2.6'

    // db
    compile 'org.flywaydb:flyway-core:4.0.3'
    compile('org.hibernate:hibernate-hikaricp:5.2.6.Final') {
        exclude group: 'javax.enterprise'
    }
    compile 'com.h2database:h2:1.4.193'

    // REST
    compile 'org.jboss.resteasy:resteasy-servlet-initializer:3.0.19.Final'
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:3.0.19.Final'
    
    // easy development with Jetty
    testCompile "org.eclipse.jetty:jetty-webapp:$jettyVer"
    testCompile "org.eclipse.jetty:jetty-annotations:$jettyVer"
    // workaround for https://github.com/Atmosphere/atmosphere/issues/978
    testCompile "org.eclipse.jetty:jetty-continuation:$jettyVer"
    // make sure that JSR356 is on classpath, otherwise Atmosphere will use native Jetty Websockets which will result
    // in ClassNotFoundException: org.eclipse.jetty.websocket.WebSocketFactory$Acceptor
    // since the class is no longer there in Jetty 9.4
    testCompile "org.eclipse.jetty.websocket:javax-websocket-server-impl:$jettyVer"

    // Embedded Undertow is currently unsupported since it has no servlet/listener/... autodiscovery capabilities:
    // http://stackoverflow.com/questions/22307748/deploying-servlets-webapp-in-embedded-undertow

    // Embedded Tomcat is currently unsupported since it always starts its own class loader which is only known on Tomcat start time
    // and we can't thus discover and preload JPA entities.
}
