plugins {
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
}

apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

repositories {
    maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
    maven { url 'https://dl.bintray.com/webjars/maven' }
}

dependencyManagement {
    imports { mavenBom "com.vaadin:vaadin-bom:$vaadin10_version" }
}

dependencies {
    compile(project(':vok-framework'))
    compile(project(':vok-db'))
    compile(project(':vok-util-vaadin10'))

    testCompile("com.github.mvysny.dynatest:dynatest:${dynatest_version}")

    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'com.h2database:h2:1.4.196'
    testCompile "com.github.kaributesting:karibu-testing-v10:$kaributesting_version"
    testCompile 'org.zeroturnaround:zt-exec:1.10'

    testCompile 'org.postgresql:postgresql:42.2.1'
    testCompile 'mysql:mysql-connector-java:5.1.45'
    testCompile 'org.mariadb.jdbc:mariadb-java-client:2.2.3'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework-v10-sql2o'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'VOK: Vaadin 10 Flow with VOK-DB persistence')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

